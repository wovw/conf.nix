# https://wiki.nixos.org/wiki/Tailscale
{
  config,
  pkgs,
  lib,
  host,
  username,
  ...
}:
let
  self = config.fleet.hosts.${host};
  inherit (self) isExitNode acceptsTailnetSsh;
in
{
  services.tailscale = lib.mkMerge [
    # Base config for every host
    {
      enable = true;
      disableUpstreamLogging = true; # disables debug logging
      useRoutingFeatures = "client";
      # Single source for the operator grant: lets $username manage
      # `tailscale serve` without sudo via the module's `tailscaled-set`
      # oneshot. Keep it here only — extraSetFlags lists concatenate across
      # modules, so defining it in each *-serve.nix duplicates --operator
      # and breaks tailscaled-set ("flag provided multiple times").
      extraSetFlags = [ "--operator=${username}" ];
    }

    # Exit-node role
    (lib.mkIf isExitNode {
      useRoutingFeatures = lib.mkForce "both";
      authKeyFile = "/var/lib/tailscale/authkey";
      extraUpFlags = [
        "--netfilter-mode=nodivert"
        "--advertise-exit-node"
      ];
    })
    (lib.mkIf acceptsTailnetSsh {
      extraUpFlags = [ "--ssh" ];
    })
  ];

  networking = {
    nftables.enable = true;
    firewall = {
      enable = true;
      # Always allow traffic from Tailscale network in NixOS firewall
      trustedInterfaces = [ config.services.tailscale.interfaceName ];
      # Allow the Tailscale UDP port through the firewall
      allowedUDPPorts = [ config.services.tailscale.port ];
    };
  };

  # Force tailscaled to use nftables (Critical for clean nftables-only systems)
  # This avoids the "iptables-compat" translation layer issues.
  systemd.services.tailscaled.serviceConfig.Environment = [
    "TS_DEBUG_FIREWALL_MODE=nftables"
  ];

  # Optimization: Prevent systemd from waiting for network online
  systemd.network.wait-online.enable = lib.mkForce false;
  boot.initrd.systemd.network.wait-online.enable = lib.mkForce false;

  # Optimize performance for high-throughput exit nodes/subnet routers
  environment.systemPackages = with pkgs; [
    ethtool
  ];
  services.udev.extraRules = ''
    # Automate UDP Generic Receive Offload (GRO) for high-throughput Tailscale routing.
    # Triggers when any ethernet or wireless interface (en*|wl*) initializes,
    # enabling packet aggregation before the CPU processes the UDP stream.
    ACTION=="add", SUBSYSTEM=="net", KERNEL=="en*|wl*", RUN+="${pkgs.ethtool}/bin/ethtool -K $name rx-udp-gro-forwarding on rx-gro-list off"
  '';
}
